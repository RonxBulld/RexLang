.版本 2

.程序集 程序集2

.子程序 _打开_被选择
.局部变量 文件号, 整数型
.局部变量 i, 整数型
.局部变量 读入文本, 文本型
.局部变量 父项目索引, 整数型
.局部变量 s, 整数型

询问保存 ()
通用对话框1.类型 ＝ 0
通用对话框1.标题 ＝ “打开文件”
通用对话框1.初始目录 ＝ 取当前目录 ()
i ＝ 1
.如果 (通用对话框1.打开 ())
    文件号 ＝ 打开文件 (通用对话框1.文件名, , )
    现行文件名 ＝ 通用对话框1.文件名
    树型框1.清空 ()
    编辑框1.内容 ＝ “”
    超级列表框1.全部删除 ()
    _启动窗口.标题 ＝ “Ini手术室-” ＋ 现行文件名
    移到文件首 (文件号)
    .判断循环首 (是否在文件尾 (文件号, ) ＝ 假)
        读入文本 ＝ 读入一行 (文件号)  ' 将文件内容读入容器
        重定义数组 (文本数组, 真, i ＋ 1)  ' 定义数组容器的上限
        文本数组 [i] ＝ 读入文本
        .如果 (取文本右边 (文本数组 [i], 1) ＝ “]”)  ' 以此判断是否为标准的ini文件
            树型框1.加入项目 (-1, 文本数组 [i], 1, , )  ' 将[]内容作为树型框的父项目
            父项目索引 ＝ i － 1
        .否则
            树型框1.加入项目 (父项目索引, 文本数组 [i], 0, , )  ' 将其他内容作为子项目
        .如果结束
        i ＝ i ＋ 1

    .判断循环尾 ()
    树型框1.扩展 (0)  ' 扩展首相
    .计次循环首 (树型框1.取项目数 (), s)  ' 将树型框内容读入超级列表框和编辑框中
        超级列表框1.插入表项 (s － 1, 树型框1.取项目文本 (s － 1), , , , )
        超级列表框1.置标题 (s － 1, 1, 树型框1.取项目文本 (s － 1))
        编辑框1.加入文本 (树型框1.取项目文本 (s － 1) ＋ #换行符)
        编辑框1.起始选择位置 ＝ 1
        .如果 (树型框1.取项目文本 (s － 1) ≠ “”)  ' 确定超级列表框的检查框选中
            超级列表框1.置状态图片 (s － 1, 1)
        .否则
            超级列表框1.置状态图片 (s － 1, 0)
        .如果结束

    .计次循环尾 ()
    日志标记 ＝ 日志标记 ＋ 1
    列表框2.加入项目 (到文本 (日志标记) ＋ “:打开文本” ＋ 现行文件名 ＋ #换行符, )  ' 以上两句将操作内容加入日志
    连续赋值 (假, 用记事本打开.禁止, 保存.禁止, 另存为.禁止, 查找.禁止, 提取所有.禁止, 提取钩选项.禁止, 提取目录树.禁止)
    工具条1.去除状态 (3, 4)
    工具条1.去除状态 (5, 4)
    工具条1.去除状态 (6, 4)
    工具条1.去除状态 (7, 4)
    工具条1.去除状态 (9, 4)
    状态条1.置文本 (1, “打开文件”)

.否则
    信息框 (“打开文件失败！”, #信息图标, “失败”)
.如果结束


.子程序 _超级列表框1_鼠标左键被放开, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型
.局部变量 m, 整数型

.如果真 (是否已修改 ＝ 真)  ' 只要是添加项目或编辑等操作后,都将对编辑框中内容进行更新
    编辑框1.内容 ＝ “”
    .计次循环首 (超级列表框1.取表项数 (), m)
        编辑框1.加入文本 (超级列表框1.取标题 (m － 1, 0) ＋ #换行符)
    .计次循环尾 ()
    编辑框1.起始选择位置 ＝ 寻找文本 (编辑框1.内容, 超级列表框1.取标题 (编辑项目索引, ), , 假) － 1
    编辑框1.被选择字符数 ＝ 取文本长度 (超级列表框1.取标题 (编辑项目索引, ))  ' 以上两句保证编辑框显示修改后的内容
.如果真结束


.子程序 _新建_被选择

.如果真 (是否已修改 ＝ 假 或 询问保存 () ＝ 真)
    清除数组 (文本数组)
    _启动窗口.标题 ＝ “Ini手术室1.0 - 无标题”
    现行文件名 ＝ “无标题”
    树型框1.清空 ()
    超级列表框1.全部删除 ()
    编辑框1.内容 ＝ “”
    用记事本打开.禁止 ＝ 假
    保存.禁止 ＝ 假
    另存为.禁止 ＝ 假
    日志标记 ＝ 日志标记 ＋ 1
    列表框2.加入项目 (到文本 (日志标记) ＋ “:新建文本-无标题” ＋ #换行符, )
    状态条1.置文本 (1, “新建文件”)
    是否已修改 ＝ 假
    连续赋值 (真, 用记事本打开.禁止, 保存.禁止, 另存为.禁止, 向上插入项.禁止, 向上插入项.禁止, 编辑选定项.禁止, 恢复当前编辑项.禁止, 恢复所有编辑项.禁止, 恢复到最初.禁止, 删除选定项.禁止, 查找.禁止, 提取所有.禁止, 提取钩选项.禁止, 提取目录树.禁止)
    工具条1.加入状态 (2, 4)
    工具条1.加入状态 (3, 4)
    工具条1.加入状态 (5, 4)
    工具条1.加入状态 (6, 4)
    工具条1.加入状态 (7, 4)
    工具条1.加入状态 (9, 4)

.如果真结束


.子程序 询问保存, 逻辑型
.局部变量 返回询问, 整数型

.如果 (是否已修改 ＝ 真)
    返回询问 ＝ 信息框 (“文件” ＋ 现行文件名 ＋ #换行符 ＋ “已被修改,是否保存更改?”, #取消是否钮, “询问”)
    .如果 (返回询问 ＝ #是钮)
        返回 (_保存_被选择 ())
    .否则
        .如果 (返回询问 ＝ #否钮)
            返回 (真)
        .否则
            返回 (假)
        .如果结束

    .如果结束

.否则
    返回 (真)
.如果结束


.子程序 _保存_被选择, 逻辑型
.参数 另存文件名, 文本型, 可空
.局部变量 文件名, 文本型
.局部变量 文件号, 整数型
.局部变量 n, 整数型

.如果 (另存文件名 ＝ “”)
    文件名 ＝ 现行文件名
    .如果真 (文件名 ＝ “无标题”)
        通用对话框1.标题 ＝ “保存”
        通用对话框1.类型 ＝ 1
        通用对话框1.文件名 ＝ 文件名
        .如果 (通用对话框1.打开 () ＝ 真)
            文件名 ＝ 通用对话框1.文件名
        .否则
            返回 (假)
        .如果结束

    .如果真结束

.否则
    文件名 ＝ 另存文件名
.如果结束
文件号 ＝ 打开文件 (文件名, #重写, )
.如果 (文件号 ≠ 0)
    写出文本 (文件号, 编辑框1.内容)
    关闭文件 (文件号)
    现行文件名 ＝ 文件名
    _启动窗口.标题 ＝ “Ini手术室1.0-” ＋ 现行文件名
    是否已修改 ＝ 假
    返回 (真)
.否则
    信息框 (“保存文件失败”, #信息图标, “失败”)
    返回 (假)
.如果结束
日志标记 ＝ 日志标记 ＋ 1
列表框2.加入项目 (到文本 (日志标记) ＋ “:保存文本” ＋ 现行文件名 ＋ #换行符, )
状态条1.置文本 (1, “保存文件”)

.子程序 __启动窗口_创建完毕

是否已修改 ＝ 假
连续赋值 (真, 用记事本打开.禁止, 保存.禁止, 另存为.禁止, 向上插入项.禁止, 向上插入项.禁止, 编辑选定项.禁止, 恢复当前编辑项.禁止, 恢复所有编辑项.禁止, 恢复到最初.禁止, 删除选定项.禁止, 查找.禁止, 提取所有.禁止, 提取钩选项.禁止, 提取目录树.禁止)
工具条1.加入状态 (2, 4)
工具条1.加入状态 (3, 4)
工具条1.加入状态 (5, 4)
工具条1.加入状态 (6, 4)
工具条1.加入状态 (7, 4)
工具条1.加入状态 (9, 4)

.子程序 _退出_被选择

.如果真 (询问保存 () ＝ 真)
    销毁 ()
.如果真结束


.子程序 __启动窗口_可否被关闭, 逻辑型

返回 (询问保存 ())

.子程序 _超级列表框1_结束编辑, 逻辑型
.参数 表项索引, 整数型

是否已修改 ＝ 真
恢复当前编辑项.禁止 ＝ 假
恢复所有编辑项.禁止 ＝ 假
恢复到最初.禁止 ＝ 假
日志标记 ＝ 日志标记 ＋ 1
列表框2.加入项目 (到文本 (日志标记) ＋ “:编辑选定项” ＋ 超级列表框1.取标题 (超级列表框1.现行选中项, ) ＋ #换行符, )
工具条1.去除状态 (2, 4)
编辑项目索引 ＝ 表项索引  ' 将现行编辑的表项索引记录,以便_超级列表框1_鼠标左键被放开()子程序调用

.子程序 _另存为_被选择

通用对话框1.标题 ＝ “另存为”
通用对话框1.类型 ＝ 1
.如果真 (通用对话框1.打开 ())
    _保存_被选择 (通用对话框1.文件名)
.如果真结束


.子程序 _向上插入项_被选择

.如果 (超级列表框1.现行选中项 ≠ -1)
    超级列表框1.插入表项 (超级列表框1.现行选中项, “新项目”, , , , )
    超级列表框1.置标题 (超级列表框1.现行选中项 － 1, 1, “新项目”)
    超级列表框1.进入编辑 (超级列表框1.现行选中项 － 1)
    日志标记 ＝ 日志标记 ＋ 1
    列表框2.加入项目 (到文本 (日志标记) ＋ “:在第” ＋ 到文本 (超级列表框1.现行选中项) ＋ “行上插入了新项目” ＋ #换行符, )
    状态条1.置文本 (1, “向上插入项”)
    是否已修改 ＝ 真
.否则
    信息框 (“请选则需要插入的位置”, 0, )
.如果结束


.子程序 _向下插入项_被选择

.如果 (超级列表框1.取表项数 () ＝ 0 或 超级列表框1.现行选中项 ≠ -1)
    超级列表框1.插入表项 (超级列表框1.现行选中项 ＋ 1, “新项目”, , , , )
    超级列表框1.置标题 (超级列表框1.现行选中项 ＋ 1, 1, “新项目”)
    超级列表框1.进入编辑 (超级列表框1.现行选中项 ＋ 1)
    日志标记 ＝ 日志标记 ＋ 1
    列表框2.加入项目 (到文本 (日志标记) ＋ “:在第” ＋ 到文本 (超级列表框1.现行选中项) ＋ “行下插入了新项目” ＋ #换行符, )
    状态条1.置文本 (1, “向下插入项”)
    是否已修改 ＝ 真
    连续赋值 (假, 用记事本打开.禁止, 保存.禁止, 另存为.禁止, 恢复到最初.禁止, 查找.禁止, 提取所有.禁止, 提取钩选项.禁止, 提取目录树.禁止)
    工具条1.去除状态 (2, 4)
    工具条1.去除状态 (3, 4)
    工具条1.去除状态 (5, 4)
    工具条1.去除状态 (6, 4)
    工具条1.去除状态 (7, 4)
    工具条1.去除状态 (9, 4)
.否则
    信息框 (“请选则需要插入的位置”, 0, )
.如果结束


.子程序 _编辑选定项_被选择

.如果 (超级列表框1.取表项数 () ＝ 0 或 超级列表框1.现行选中项 ≠ -1)
    超级列表框1.进入编辑 (超级列表框1.现行选中项)
    .如果真 (是否已修改 ＝ 真)
        日志标记 ＝ 日志标记 ＋ 1
        列表框2.加入项目 (到文本 (日志标记) ＋ “:编辑选定项” ＋ 超级列表框1.取标题 (超级列表框1.现行选中项, 0) ＋ #换行符, )
        状态条1.置文本 (1, “编辑选定项”)
    .如果真结束

.否则
    信息框 (“请选则需要插入的位置”, 0, )

.如果结束


.子程序 _超级列表框1_被双击, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

编辑框1.起始选择位置 ＝ 寻找文本 (编辑框1.内容, 超级列表框1.取标题 (超级列表框1.现行选中项, ), , 假) － 1
编辑框1.被选择字符数 ＝ 取文本长度 (超级列表框1.取标题 (超级列表框1.现行选中项, ))  ' 实现编辑框和列表框显示内容同步

.子程序 _超级列表框1_左键单击表项

.如果真 (超级列表框1.取表项数 () ≠ 0)
    选择夹1.现行子夹 ＝ 0  ' 立刻显示文本内容
    .如果真 (超级列表框1.现行选中项 ≠ -1)
        编辑选定项.禁止 ＝ 假
        向上插入项.禁止 ＝ 假
        向下插入项.禁止 ＝ 假
        删除选定项.禁止 ＝ 假
        _启动窗口.刷新显示 ()
        状态条1.置文本 (0, “当前选定第” ＋ 到文本 (超级列表框1.现行选中项 ＋ 1) ＋ “行”)
        .如果真 (是否已修改 ＝ 真)
            恢复当前编辑项.禁止 ＝ 假
        .如果真结束

    .如果真结束
    状态条1.置文本 (1, “列表项;双击,编辑项目,文本定位到相应行”)
.如果真结束


.子程序 _恢复当前编辑项_被选择
.局部变量 恢复数据, 文本型

.如果 (超级列表框1.现行选中项 ≠ -1)
    .如果 (超级列表框1.取标题 (超级列表框1.现行选中项, 0) ≠ 超级列表框1.取标题 (超级列表框1.现行选中项, 1))
        恢复数据 ＝ 超级列表框1.取标题 (超级列表框1.现行选中项, 1)
        超级列表框1.置标题 (超级列表框1.现行选中项, 0, 恢复数据)
        日志标记 ＝ 日志标记 ＋ 1
        列表框2.加入项目 (到文本 (日志标记) ＋ “:恢复当前编辑项” ＋ 超级列表框1.取标题 (超级列表框1.现行选中项, ) ＋ #换行符, )
        状态条1.置文本 (1, “恢复当前编辑项”)
        是否已修改 ＝ 真
    .否则
        信息框 (“当前项目未经过编辑,请选择编辑过得项目”, 0, )
    .如果结束

.否则
    信息框 (“请选择需要恢复项目!”, 0, )
.如果结束


.子程序 _恢复所有编辑项_被选择
.局部变量 n, 整数型

.如果 (信息框 (“真的要恢复所有修改吗?”, 33, “询问”) ＝ 0)
    .计次循环首 (超级列表框1.取表项数 (), n)
        超级列表框1.置标题 (n － 1, 0, 超级列表框1.取标题 (n － 1, 1))
    .计次循环尾 ()
.否则
    返回 ()
.如果结束
日志标记 ＝ 日志标记 ＋ 1
列表框2.加入项目 (到文本 (日志标记) ＋ “:恢复所有编辑项” ＋ #换行符, )
状态条1.置文本 (1, “恢复所有编辑项”)
是否已修改 ＝ 真

.子程序 _删除选定项_被选择
.局部变量 m, 整数型

.如果 (超级列表框1.现行选中项 ≠ -1)
    日志标记 ＝ 日志标记 ＋ 1
    列表框2.加入项目 (到文本 (日志标记) ＋ “:删除选定项” ＋ 超级列表框1.取标题 (超级列表框1.现行选中项, ) ＋ #换行符, )
    超级列表框1.删除表项 (超级列表框1.现行选中项)
    是否已修改 ＝ 真
    状态条1.置文本 (1, “删除选定项”)
.否则
    信息框 (“请选则需要删除的项目”, 0, )
.如果结束


.子程序 _超级列表框1_鼠标右键被按下, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

弹出菜单 (编辑, , )

.子程序 _超级列表框1_鼠标左键被按下, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

超级列表框1.选择表项 (超级列表框1.现行选中项)
编辑框1.被选择字符数 ＝ 0
.如果真 (超级列表框1.现行选中项 ＝ -1)
    编辑选定项.禁止 ＝ 真
    向上插入项.禁止 ＝ 真
    向下插入项.禁止 ＝ 真
    删除选定项.禁止 ＝ 真
.如果真结束


.子程序 _恢复到最初_被选择
.局部变量 n, 整数型

超级列表框1.全部删除 ()
编辑框1.内容 ＝ “”
.计次循环首 (取数组成员数 (文本数组), n)
    超级列表框1.插入表项 (n － 1, 文本数组 [n], , , , )
    超级列表框1.置标题 (n － 1, 1, 文本数组 [n])
    .如果 (树型框1.取项目文本 (n － 1) ≠ “”)
        超级列表框1.置状态图片 (n － 1, 1)
    .否则
        超级列表框1.置状态图片 (n － 1, 0)
    .如果结束

    编辑框1.加入文本 (树型框1.取项目文本 (n － 1) ＋ #换行符)
    编辑框1.起始选择位置 ＝ 1
.计次循环尾 ()
日志标记 ＝ 日志标记 ＋ 1
列表框2.加入项目 (到文本 (日志标记) ＋ “:恢复到最初状态” ＋ #换行符, )
状态条1.置文本 (1, “恢复到最初”)

.子程序 _用记事本打开_被选择

运行 (“c:\windows\notepad.exe ” ＋ 现行文件名, 假, )

.子程序 _工具条1_被单击
.参数 按钮索引, 整数型

.如果真 (按钮索引 ＝ 0)
    _新建_被选择 ()
.如果真结束
.如果真 (按钮索引 ＝ 1)
    _打开_被选择 ()
.如果真结束
.如果真 (按钮索引 ＝ 2)
    _保存_被选择 ()
.如果真结束
.如果真 (按钮索引 ＝ 3)
    _另存为_被选择 ()
.如果真结束
.如果真 (按钮索引 ＝ 5)
    _提取所有_被选择 ()
.如果真结束
.如果真 (按钮索引 ＝ 6)
    _提取钩选项_被选择 ()
.如果真结束
.如果真 (按钮索引 ＝ 7)
    _提取目录树_被选择 ()
.如果真结束
.如果真 (按钮索引 ＝ 9)
    _查找_被选择 ()
.如果真结束


.子程序 _树型框1_鼠标左键被按下, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

状态条1.置文本 (1, “目录树.单击,列表定位到相应项”)

.子程序 _树型框1_项目被选择
.参数 选择方式, 整数型

超级列表框1.保证显示 (树型框1.现行选中项)
超级列表框1.选择表项 (树型框1.现行选中项)
编辑框1.被选择字符数 ＝ 0

.子程序 _树型框1_鼠标位置被移动, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

状态条1.置文本 (1, “目录树.单击,列表定位到相应项”)

.子程序 _超级列表框1_鼠标位置被移动, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

状态条1.置文本 (1, “列表项,双击,文本定位到相应行”)

.子程序 _选择夹1_鼠标左键被按下, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

.如果 (选择夹1.现行子夹 ＝ 1)
    .如果 (编辑框1.内容 ≠ “”)
        状态条1.置文本 (1, “当前文本内容于列表项同”)
    .否则
        状态条1.置文本 (1, “目标文本”)
    .如果结束

.否则
    状态条1.置文本 (1, “日志”)
.如果结束


.子程序 _列表框2_列表项被选择

状态条1.置文本 (1, 列表框2.取项目文本 (列表框2.现行选中项))

.子程序 __启动窗口_尺寸被改变

树型框1.宽度 ＝ 取用户区宽度 () × 0.3 － 5
树型框1.高度 ＝ 取用户区高度 () － 状态条1.高度
超级列表框1.左边 ＝ 树型框1.宽度
超级列表框1.宽度 ＝ 取用户区宽度 () － 树型框1.宽度 － 5
超级列表框1.高度 ＝ (取用户区高度 () － 状态条1.高度) ÷ 2
超级列表框1.置列宽 (0, 超级列表框1.宽度 ÷ 2)
超级列表框1.置列宽 (1, 超级列表框1.宽度 ÷ 2)
选择夹1.左边 ＝ 树型框1.宽度
选择夹1.宽度 ＝ 取用户区宽度 () － 树型框1.宽度 － 5
选择夹1.高度 ＝ 取用户区高度 () － 超级列表框1.高度 － 状态条1.高度 － 35
选择夹1.顶边 ＝ 超级列表框1.高度 ＋ 35
列表框2.宽度 ＝ 选择夹1.宽度 － 8
列表框2.高度 ＝ 选择夹1.高度 － 30
编辑框1.宽度 ＝ 选择夹1.宽度 － 8
编辑框1.高度 ＝ 选择夹1.高度 － 30
状态条1.宽度 ＝ 取用户区宽度 ()
状态条1.置宽度 (0, 取用户区宽度 () × 0.2)
状态条1.置宽度 (1, 取用户区宽度 () × 0.8)

.子程序 _提取所有_被选择
.局部变量 i, 整数型

编辑框1.内容 ＝ “”
.计次循环首 (超级列表框1.取表项数 (), i)
    编辑框1.加入文本 (超级列表框1.取标题 (i － 1, 0) ＋ #换行符)
.计次循环尾 ()
状态条1.置文本 (1, “提取所有”)
日志标记 ＝ 日志标记 ＋ 1
列表框2.加入项目 (到文本 (日志标记) ＋ “:提取所有项” ＋ #换行符, )
编辑框1.起始选择位置 ＝ 1

.子程序 _提取钩选项_被选择
.局部变量 i, 整数型

编辑框1.内容 ＝ “”
.计次循环首 (超级列表框1.取表项数 (), i)
    .如果真 (超级列表框1.取当前状态图片 (i － 1) ＝ 1)
        编辑框1.加入文本 (超级列表框1.取标题 (i － 1, 0) ＋ #换行符)
    .如果真结束

.计次循环尾 ()
状态条1.置文本 (1, “提取钩选项”)
日志标记 ＝ 日志标记 ＋ 1
列表框2.加入项目 (到文本 (日志标记) ＋ “:提取钩选项” ＋ #换行符, )
编辑框1.起始选择位置 ＝ 1

.子程序 _提取目录树_被选择
.局部变量 i, 整数型

编辑框1.内容 ＝ “”
.计次循环首 (树型框1.取项目数 (), i)
    编辑框1.加入文本 (树型框1.取项目文本 (i － 1) ＋ #换行符)
.计次循环尾 ()
状态条1.置文本 (1, “提取目录树”)
日志标记 ＝ 日志标记 ＋ 1
列表框2.加入项目 (到文本 (日志标记) ＋ “:提取目录树” ＋ #换行符, )
编辑框1.起始选择位置 ＝ 1

.子程序 _编辑框1_鼠标左键被按下, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

状态条1.置文本 (1, “文本查看;为最终保存文本”)

.子程序 _编辑框1_鼠标位置被移动, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

.如果 (编辑框1.内容 ≠ “”)
    状态条1.置文本 (1, “当前文本内容于列表项同”)
.否则
    状态条1.置文本 (1, “”)
.如果结束


.子程序 _查找_被选择

载入 (查找窗口, _启动窗口, 真)

.子程序 _关于_被选择

载入 (关于窗口, , 真)

.子程序 子程序1
.参数 参考, 整数型, 参考
.参数 参考可空, 字节型, 参考 可空
.参数 参考可空数组, 字节型, 参考 可空 数组
.参数 可空, 字节型, 可空
.参数 可空数组, 字节型, 可空 数组
.参数 数组, 字节型, 数组
.参数 备注, 字节型, , 备注
.参数 参考备注, 整数型, 参考, 备注
.参数 参考可空备注, 字节型, 参考 可空, 备注
.参数 参考可空数组备注, 字节型, 参考 可空 数组, 备注
.参数 可空备注, 字节型, 可空, 备注
.参数 可空数组备注, 字节型, 可空 数组, 备注
.参数 数组备注, 字节型, 数组, 备注


